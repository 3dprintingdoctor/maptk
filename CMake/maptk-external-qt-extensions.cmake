# Central location for MAPTK external dependency declaration and resolution

message(STATUS "Configuring external qtExtensions")


set( QT_QMAKE_EXECUTABLE "" CACHE FILEPATH "Path to QT QMake" )
if(IS_DIRECTORY ${fletch_DIR} AND NOT fletch_FOUND AND NOT QT_QMAKE_EXECUTABLE)
  set(QT_QMAKE_EXECUTABLE "${fletch_DIR}/install/bin/qmake" CACHE FILEPATH "Used to find where Qt is installed" FORCE)
endif()

if(NOT QT_QMAKE_EXECUTABLE)
  message(FATAL_ERROR "QT is required, Please set QT_QMAKE_EXECUTABLE")
endif()

ExternalProject_Add(qtExtensions
  DEPENDS ${QT_EXTENSIONS_DEPENDENCIES}
  PREFIX ${MAPTK_BINARY_DIR}
  SOURCE_DIR qtExtensions
  BINARY_DIR qtExtensions-build
  STAMP_DIR ${MAPTK_STAMP_DIR}
  GIT_REPOSITORY "https://github.com/Kitware/qtextensions.git"
  GIT_TAG de142f71190cbff9427ad1bbdcb340b34ec595bd
  #GIT_SHALLOW 1
  CMAKE_CACHE_ARGS
    -DBUILD_SHARED_LIBS:BOOL=ON
    -DQT_QMAKE_EXECUTABLE:FILEPATH=${QT_QMAKE_EXECUTABLE}
    -DCMAKE_PREFIX_PATH:STRING=${CMAKE_PREFIX_PATH}
    -DCMAKE_BUILD_TYPE:STRING=${CMAKE_BUILD_TYPE}
    -DCMAKE_CXX_COMPILER:FILEPATH=${CMAKE_CXX_COMPILER}
    -DCMAKE_CXX_FLAGS:STRING=${CMAKE_CXX_FLAGS}
    -DCMAKE_C_COMPILER:FILEPATH=${CMAKE_C_COMPILER}
    -DCMAKE_C_FLAGS:STRING=${CMAKE_C_FLAGS}
    ${CMAKE_CXX_COMPILER_LAUNCHER_FLAG}
    ${CMAKE_C_COMPILER_LAUNCHER_FLAG}
    -DCMAKE_EXE_LINKER_FLAGS:STRING=${CMAKE_EXE_LINKER_FLAGS}
    -DCMAKE_SHARED_LINKER_FLAGS:STRING=${CMAKE_SHARED_LINKER_FLAGS}
    -DMAKECOMMAND:STRING=${MAKECOMMAND}
    -DADDITIONAL_C_FLAGS:STRING=${ADDITIONAL_C_FLAGS}
    -DADDITIONAL_CXX_FLAGS:STRING=${ADDITIONAL_CXX_FLAGS}
  INSTALL_COMMAND cmake -E echo "Skipping install step."
  #INSTALL_DIR "${CMAKE_INSTALL_PREFIX}"
)
list(APPEND MAPTK_DEPENDENCIES qtExtensions)

set(qtExtensions_DIR "${MAPTK_BINARY_DIR}/qtExtensions-build")
